# Copyright 1999-2023 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

# ebuild generated by hackport 0.8.3.0

CABAL_PN="pLam"

CABAL_FEATURES="lib profile haddock hoogle hscolour"
inherit haskell-cabal

DESCRIPTION="An interpreter for learning and exploring pure Î»-calculus"
HOMEPAGE="https://github.com/sandrolovnicki/pLam"
SRC_URI="https://github.com/slovnicki/pLam/archive/refs/tags/v${PV}.tar.gz -> ${P}.tar.gz"

LICENSE="BSD"
SLOT="0/${PV}"
KEYWORDS="~amd64"
IUSE="examples"

RDEPEND="dev-haskell/ansi-terminal:=[profile?]
	dev-haskell/haskeline:=[profile?]
	dev-haskell/optparse-applicative:=[profile?]
	dev-haskell/parsec:=[profile?]
	>=dev-lang/ghc-8.8.1:=
"
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-3.0.0.0
"

PATCHES=("${FILESDIR}/${P}-gentoo.patch")

src_prepare() {
	cat >"${S}/src/Config.hs" <<-EOF
	module Config where

	importPath = "/usr/share/doc/${PF}/import/"
	EOF

	haskell-cabal_src_prepare
}

src_install() {
	haskell-cabal_src_install

	docompress -x /usr/share/doc/${PF}/import
	dodoc -r import/

	if use examples; then
		docompress -x /usr/share/doc/${PF}/examples
		dodoc -r examples/
	fi
}
